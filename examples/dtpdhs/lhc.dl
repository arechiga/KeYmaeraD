{
 x : (C) -> Real,
 v : (C) -> Real,
 a : (C) -> Real,
 e : (C) -> Real,
 n : () -> C,
 t : () -> Real
}
B > 0,
A > 0, 
(forall i : C . e(i) = 1 ==> v(i) >= 0),
forall f : C. 
 forall l : C. 
  (e(f) = 1 & e(l) = 1 & x(f) <= x(l) &  (~ (f = l))) ==>
        (2 * B * x(l) > 2 *  B * x(f) + v(f) ^2 - v(l)^2  & 
        x(f) < x(l)  
        )
|-
[
(n :=*; ?(e(n) = 1); e(n) := 0)
++
(n :=*; ?(e(n) = 0 & v(n) >= 0); e(n) := 1; 
 ?(forall i : C. 
    (e(i) = 1 ==> 
      ((x(i) < x(n) ==>  
        2 * B * x(n) > 2 *  B * x(i) + v(i)^2 - v(n)^2 
                       + (A + B) * (A * eps^2 + 2 * eps * v(i)))
      & (x(n) < x(i) ==>  
        2 * B * x(i) > 2 *  B * x(n) + v(n)^2 - v(i)^2 
                       + (A + B) * (A * eps^2 + 2 * eps * v(n)))))))
++
(
forall f : C a(f) := *;
?(forall f : C.
 e(f) = 1 ==>
 (a(f) = -B |
  ( v(f) = 0 & a(f) = 0) |
  ((forall l : C. 
    (e(l) = 1 & x(f) <= x(l) &  (~ (f = l)) 
      ) ==> 
         2 * B * x(l) > 2 *  B * x(f) + v(f) ^2 - v(l)^2 
                       + (A + B) * (A * eps^2 + 2 * eps * v(f)))
   & a(f) = A))
 );
   t:= 0; forall i : C {x(i)' = v(i), 
                        v(i)' = a(i), 
                        t()' = 1; (e(i) = 1 ==> v(i) >= 0) &  t<= eps}  
                         @solution( forall s . x(s, i) = (1/2) *a(i) * s^2 + v(i) * s + x(i),
                                    forall s . v(s, i) = a(i) * s + v(i),
                                    forall s . t(s) = t()  + s)
)
]
(
(forall i : C. e(i) = 1 ==> v(i) >= 0)
&
(
forall f : C. 
  forall l : C. 
   (e(f) = 1 & e(l) = 1 & x(f) <= x(l) &  (~ (f = l)))  ==>
         (2 * B * x(l) > 2 *  B * x(f) + v(f) ^2 - v(l)^2 &
        x(f) < x(l)    
         )
)
)

